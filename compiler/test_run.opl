#define TEST_INT(want, got) testInt(want, got, #got)
var (passed int; failed int;)

func main() {
  TEST_INT(42, testIntConstant());
  TEST_INT(11, testAddSub());
  TEST_INT(2 , testMulDiv());
  TEST_INT(5 , testUnaryOp());
  TEST_INT(0 , testRelOp());
  TEST_INT(1 , testRelOp2());
  TEST_INT(1 , testEqOp());
  TEST_INT(0 , testBlankBlock());
  TEST_INT(3 , testCompoundBlock());
  TEST_INT(15, testDefVar());
  TEST_INT(3 , testReturn());
  TEST_INT(2 , testIf());
  TEST_INT(1 , testIfVar());
  TEST_INT(8 , testAssign());
  TEST_INT(38, testAssign2());
  TEST_INT(42, testAssign3());
  TEST_INT(55, testFor());
  TEST_INT(9 , testAssign4());
  TEST_INT(55, testFor2());
  TEST_INT(4 , testElse());
  TEST_INT(5 , testElseIf());
  TEST_INT(42, testSimpleBlock());
  TEST_INT(9 , testNextedFor());
  TEST_INT(39, testExternalFunc());
  TEST_INT(42, testExternalFunc2());
  TEST_INT(4 , testFuncArg());
  TEST_INT(57, testFuncArg2());
  TEST_INT(5 , testDefFunc());
  TEST_INT(21, testRecursion());
  TEST_INT(5 , testPtrWrite());
  TEST_INT(4 , testPtrRead());
  TEST_INT(14, testPtrPtr());
  TEST_INT(3 , testPtrFunc());
  TEST_INT(42, testUnaryOp2());
  TEST_INT(4 , testVarPtr());
  TEST_INT(42, testVarFuncPtr());
  TEST_INT(16, testPtrAddInt());
  TEST_INT(24, testPtrSubInt());
  TEST_INT(2 , testPtrSubPtr());
  TEST_INT(4 , testArrayPtr());
  TEST_INT(16, testSizeof());
  TEST_INT(4 , testIntArray());
  TEST_INT(7 , testFuncArray());
  TEST_INT(42, testGVar());
  TEST_INT(10, testGVarInit());
  TEST_INT(1 , testSizeofByte());
  TEST_INT(43, testVarByte());
  TEST_INT(2 , testVarInt64());
  TEST_INT(0 , testOverflow());
  TEST_INT(2 , testOverflow2());
  TEST_INT(4 , testInt8Array());
  TEST_INT(10, testInt16Ptr());
  TEST_INT(28, testInt32Ptr());
  TEST_INT(33, testStr());
  TEST_INT(5 , testStrlen());
  TEST_INT(6 , testSizeofStr());
  TEST_INT(10, testStrEscape());
  TEST_INT(4 , testPtrInt8());
  TEST_INT(33, testPtrChar());
  TEST_INT(1 , testPtrChar2());
  TEST_INT(1 , testVarInt8());
  TEST_INT(12, testCompoundAssign());
  TEST_INT(33, testAsciiExpr());
  TEST_INT(4 , testLogicalOp());
  TEST_INT(1 , testLogicalOp2());
  TEST_INT(2 , testLogicalOp3());
  TEST_INT(2 , testComment());
  TEST_INT(3 , testBreak());
  TEST_INT(10, testContinue());
  TEST_INT(12, testContinue2());
  TEST_INT(10, testBreak2());
  TEST_INT(42, testBreak3());
  TEST_INT(4 , testSizeofUserType());
  TEST_INT(4 , testUserType());
  TEST_INT(3,  testInt8Overflow());
  TEST_INT(5,  testUInt());
  TEST_INT(1,  testUInt2());
  TEST_INT(4,  testIncDec());
  TEST_INT(1,  testNonByteType());
  TEST_INT(2,  testNonByteType2());
  TEST_INT(7,  testVariadicArg());
  TEST_INT(31, testHexLiteral());
  TEST_INT(6,  testBinaryLiteral());
  TEST_INT(7,  testArrayInit());
  TEST_INT(3,  testStruct());
  TEST_INT(-3, testStruct2());
  TEST_INT(5,  testStructPtr());
  TEST_INT(6,  testStructInit());
  TEST_INT(11, testStructType());
  TEST_INT(8,  testUserTypeParam());
  TEST_INT(5,  testStructArrowOp());
  TEST_INT(8,  testStructArg());
  TEST_INT(42, testRecursiveType());
  TEST_INT(42, testRecursiveType());
  TEST_INT(0x10f, testGVarInitList());
  TEST_INT(5,  testArrayLiteral());
  TEST_INT(15, testStructLiteral());
  TEST_INT(5,  testStructLiteral2());
  TEST_INT(21, testStructLiteral3());

  printf("%ld passed, %ld failed\n", passed, failed);
  return failed > 0;
}

func testInt(want, got int, gots *byte) {
  if want == got {
    printf("[  OK  ]: ");
    passed += 1;
  } else {
    printf("[FAILED]: ");
    failed += 1;
  }

  printf("%s -> %ld", gots, got);

  if want != got {
    printf(", want %ld", want);
  }
  printf("\n");
}

func testIntConstant() int { 42; }
func testAddSub() int { 1+23 - 13; }
func testMulDiv() int { 12/2 - 2 * (3 - 1); }
func testUnaryOp() int { -3 + (+8); }
func testRelOp() int { 3 < 1; }
func testRelOp2() int { 2*3 >= 13/2; }
func testEqOp() int { 2>2 == 4<=3; }
func testBlankBlock() int { }
func testCompoundBlock() int { 42; 3; }
func testDefVar() int { foo:=5; bar:=3; foo*bar; }
func testReturn() int { return 3; 42; }
func testIf() int { 1; if 42 > 10 { 2; } }
func testIfVar() int { cond := 10 < 1; 1; if cond { 2; } }
func testAssign() int { foo := 3; foo = 4; foo * 2; }
func testAssign2() int { foo:=5; bar:=7; foo=(bar=1)=42; foo-4; }
func testAssign3() int { foo:=5; bar:=7; foo=bar=42; }
func testFor() int { i:=0; s:=0; for i <= 10 { s=s+i; i=i+1; } s; }
func testAssign4() int { a:=5; a=b:=3; a*b; }
func testFor2() int { s:=0; for i:=0; i<=10; i=i+1 { s=s+i; } }
func testElse() int { if 0 { 3; } else { 4; } }
func testElseIf() int { if 0 { 3; } else if 1 { 5; } else { 4; } }
func testSimpleBlock() int { 42; {} }
func testNextedFor() int { s:=0; for i:=1;i<3;i=i+1{ for j:=1;j<3;j=j+1{ s=s+i*j; } } s; }
func testExternalFunc() int { func42() - 3; }
func testExternalFunc2() int { funcfunc42()(); }
func testFuncArg() int { add(add(1, -2), 5); }
func testFuncArg2() int{add(sum10(), 2);}
func testDefFunc() int{myAdd(-3,8);}
func testRecursion() int{fib(8);}
func testPtrWrite() int{ i := 42; p := &i; *p = 3; i + 2; }
func testPtrRead() int{ i := 42; p := &i; i = 3; j := *p; j + 1; }
func testPtrPtr() int{ i := 7; p := &i; q := &p; **q + *&i; }
func testPtrFunc() int{ i:=42; ptrWrite(&i); i; }
func testUnaryOp2() int { -+-42; }
func testVarPtr() int { var i int; var p *int = &i; i = 42; i = 3; *p + 1; }
func testVarFuncPtr() int { var f *func() func() int; f = &funcfunc42; f()(); }
func testPtrAddInt() int { var p *int = 8; p+1; }
func testPtrSubInt() int { var p *int = 32; p-1; }
func testPtrSubPtr() int { var p *int = 8; var q *int = 24; q-p; }
func testArrayPtr() int { p:=alloc4(3,4,5,6); *(p+1); }
func testSizeof() int { var i int; 3*sizeof(int) - sizeof(42); }
func testIntArray() int { var arr [3]int; for i:=0; i<3; i=i+1 { arr[i]=i*2; } arr[2]; }
func testFuncArray() int { var arr [3]*func(a,b int); arr[1]=&add; arr[1](3,4); }
func testGVar() int { var (v int = val*3;) v; }
func testGVarInit() int { ga=3; ga+gc; }
func testSizeofByte() int { sizeof(byte); }
func testVarByte() int { var c byte = 42; c+1; }
func testVarInt64() int { var c int64 = 255; c=c+1; c/100; }
func testOverflow() int { var c int8 = 255; c=c+1; c/100; }
func testOverflow2() int { var c byte = 255; i:=c@int+1; i/100; }
func testInt8Array() int { var arr [3]int8; arr[0]=-1; arr[1]=5; arr[2]=arr[0]+arr[1]; }
func testInt16Ptr() int { var p *int16 = 8; p+1; }
func testInt32Ptr() int { var p *int32 = 32; p-1; }
func testStr() int { "abc!def"[3]; }
func testStrlen() int { strlen("world"); }
func testSizeofStr() int { sizeof("abcdef"); }
func testStrEscape() int { "\n"[0]; }
func testPtrInt8() int { var x [2]int8; x[0]=1; x[1]=2; p:=&x[0]; p[1]=3; x[1]+1; }
func testPtrChar() int { p := &" !"[0]; p[1]; }
func testPtrChar2() int { p := &" !"[0]; if p[0]==32 { 1; } else { 2; } }
func testVarInt8() int { var a [1]int8; var b int8 = a[0]; a[0]=1; b=2; a[0]; }
func testCompoundAssign() int { i := 1; i += 2; j := 3; i *= 1 + j; }
func testAsciiExpr() int { '!' + '\t' - 9; }
func testLogicalOp() int { i := 3; if i==1 && 2==1 || i==3 { 4; } else { 5; } }
func testLogicalOp2() int { v := 5 && 6 || 7; }
func testLogicalOp3() int { v := 2; 42 || (v = 3); v; }
/* test func */ func testComment() int { 2; } // comment
func testBreak() int { i := 1; for i < 10 { if i == 3 { break; } i += 1; } i; }
func testContinue() int { i := 1; for i < 10 { i = 10; continue; i = 42; } i; }
func testContinue2() int { i := 1; for i = 1; i < 12; i += 1 { continue; } i; }
func testBreak2() int { i := 1; for { if i >= 10 { break; } i = 10; continue; i = 42; } i; }
func testBreak3() int { i := 1; for { if i >= 42 { break; } i += 1; continue; } i; }
func testSizeofUserType() int { sizeof(myInt); }
func testUserType() int { var v myInt; v = 3@myInt; v + 2@myInt - 1@myInt; }
func testInt8Overflow() int { var (i int8=2; j int8=-1;) i-j; }
func testUInt() int { var i uint = 7; i - 2@uint; }
func testUInt2() int { var i uint = -1; i > 3@uint; }
func testIncDec() int { i := 3; i++; i++; i--; i; }
func testNonByteType() int { var i uint2 = 2; i + 3@uint2; }
func testNonByteType2() int { 10@uint3; }
func testVariadicArg() int { variadic_sum(3, 4, 5, -2); }
func testHexLiteral() int { 0x001f; }
func testBinaryLiteral() int { 0b0110; }
func testArrayInit() int { var x [3]int = {3, 4}; x[0] + x[1] + x[2]; }
func testStruct() int { var x struct { a int; }; x.a = 2; x.a + 1; }
func testStruct2() int { var x struct{a int;b int;}; x.a=2; x.b=5; x.a-x.b; }
func testStructPtr() int { var p *struct{a int;}; var x struct{a int;}; p=&x; x.a=2; (*p).a+3;}
func testStructInit() int { var x struct{a int;b int;c int;} = {5, -1}; x.a-x.b+x.c; }
func testStructType() int { var x myIntStruct = {3, 8}; x.c = x.a + x.b; x.c; }
func testUserTypeParam() int { var x myInt = 5; myAdd32(3@myInt, x); }
func testStructArrowOp() int { var x myIntStruct = {3, 2}; p := &x; p->a + x.b; }
func testStructArg() int { var x myIntStruct = {7}; myIncField(&x); x.a; }
func testRecursiveType() int { var x myNode = {42}; var y myNode = {2, &x}; y.next->value; }
func testGVarInitList() int { gnode.value + gnode.next@int; }
func testArrayLiteral() int { [3]int{3, 5, 7}[1]; }
func testStructLiteral() int { 2 * struct {a int; b int; c int;}{3, 7, 5}.b + 1; }
func testStructLiteral2() int { x := struct {a int32; b int8;}{-3, 4}; x.b + 1; }
func testStructLiteral3() int { var x struct {a int32; b int8;}; x = struct {a int32; b int8;}{3, 7}; x.a * x.b; }

extern "C" func42 func()int;
extern "C" funcfunc42 func() *func();
extern "C" add func(a, b int) int;
func sum10()int{s:=0; for i:=1;i<=10;i=i+1 {s=s+i;} s;}
func myAdd(a,b int)int{a+b;}
func fib(n int)int{ if n<=1 {n;} else {fib(n-1)+fib(n-2);} }
func ptrWrite(p *int){ *p = 3; }
extern "C" alloc4 func(a, b, c, d int) *int;
var val int = 14;
var (ga int; gb = 6; gc = gb+1;)
extern "C" strlen func(s *byte)int64;
func myAdd32(a, b myInt) myInt { a + b; } // use myInt before type definition
type myInt int32;
extern "C" variadic_sum func(argc int, ...) int;
type myIntStruct struct { a int; b int; c int; };
func myIncField(x *myIntStruct) { x->a++; }
type myNode struct { value int; next *myNode; };
var (gnode myNode = {15,0x100};)

//test_stdout 'foo' 'func main() { write(1,"foo",3); } extern "C" write func(fd int32,s *byte,n int64);'

extern "C" printf func(format *byte, ...) int;
